def srcHookPath = "${rootDir}/.githooks/pre-commit"
def hookFilePath = "${rootDir}/.git/hooks/pre-commit"

tasks.register("installGitHook") {
    def srcHook = file(srcHookPath)
    def hookFile = file(hookFilePath)

    outputs.file(hookFile)

    doLast {
        if (!srcHook.exists()) {
            println "No hook found in .githooks/pre-commit"
            return
        }

        hookFile.parentFile.mkdirs()
        hookFile.text = srcHook.text
        hookFile.setExecutable(true)
        println "Pre-commit was installed/updated in .git/hooks/"
    }
}


subprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'jacoco'

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:2.0.0"
        testImplementation "org.jetbrains.kotlin:kotlin-test:2.0.0"
        testImplementation "org.junit.jupiter:junit-jupiter:5.9.2"
        testImplementation "io.mockk:mockk:1.13.7"
        implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.15.2'
    }

    compileKotlin {
        kotlinOptions { jvmTarget = "21" }
    }
    compileTestKotlin {
        kotlinOptions { jvmTarget = "21" }
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.required = true
            html.required = true
        }
    }

    jacocoTestCoverageVerification {
        dependsOn jacocoTestReport
        violationRules {
            rule {
                limit { minimum = 0.0 }
            }
        }
    }

    tasks.named("test") {
        dependsOn(rootProject.tasks.named("installGitHook"))
    }
}

tasks.matching { it.name == "test" }.configureEach {
    dependsOn(tasks.named("installGitHook"))
}