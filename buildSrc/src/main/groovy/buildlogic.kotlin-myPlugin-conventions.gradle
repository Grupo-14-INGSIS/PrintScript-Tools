plugins {
    id 'org.jetbrains.kotlin.jvm' //sin version
    id 'org.jlleitschuh.gradle.ktlint'
    id 'jacoco'
    id 'maven-publish'
}

version = '1.1.42'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

test {
    useJUnitPlatform()
}

tasks.named("build") {
    dependsOn "ktlintCheck", "test" //no pongo el de formatt pq cambiaria el archivo y no quier que lo haga aca.  Lo corro automatico, pero desde la terminal.
}

ktlint{
    additionalEditorconfigFile = file("$rootDir/config/.editorConfig") // con esto tenemos las reglas custom
}

//cargo las variables automaticamente desde el env
def dotenvFile = file("$rootDir/.env")
def dotenvProps = new Properties()
if (dotenvFile.exists()) {
    dotenvFile.withReader { reader ->
        reader.eachLine { line ->
            line = line.trim()
            if (!line.startsWith("#") && line.contains("=")) {
                def (key, value) = line.split("=", 2)
                dotenvProps.setProperty(key.trim(), value.trim())
            }
        }
    }
}

// para publicar objetos se usa el siguiente bloque. Reemplazar password pero el valor del token que eseta en el .env
publishing {
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }

    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/Grupo-14-INGSIS/PrintScript-Tools')
            credentials {
                username = dotenvProps.getProperty("GITHUB_USER") ?: ""
                password = dotenvProps.getProperty("GITHUB_TOKEN") ?: ""
            }
        }
    }
}
